@  Апплет чтения данных из памяти модема через команду 11
@
@ На входе:
@ R0 = адрес начала данного кода (точки start)
@ R1 = адрес ответного буфера
@
@

      .org    0	
      .byte   0,0 	  @ выравнивающие байты - отрезаются от объектного модуля
      .byte   0x11,0      @ код команды 11 и выравнивающий байт - это остается в объектном модуле

start: 
       LDR    R3,srcadr           @ R3 = адрес читаемых данных в памяти модема
       LDR    R4,lenadr           @ R4 = размер записываемых данных в байтах
       MOV    R0,#0x12            @ код ответа 
       STR    R0,[R1],#4          @ сохраняем его в ответный буфер
       ADD    R0,R3,R4            @ R0 = адрес конца блока данных

locloop:   
       LDR    R2,[R3],#4	  
       STR    R2,[R1],#4
       CMP    R3,R0
       BCC    locloop
       ADD    R4,#8               @ размер ответа - 4 байта кода и данные
       BX     LR

srcadr:.word  0
lenadr:.word  0
